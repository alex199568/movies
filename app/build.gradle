apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

apply plugin: 'jacoco'

apply plugin: 'kotlin-kapt'

jacoco {
  toolVersion = "0.8.0"
  reportsDir = file("$buildDir/reports")
}

android {
  compileSdkVersion 27
  signingConfigs {
    release {
      storeFile file("./release.jks")
      storePassword System.getenv("STORE_PASSWORD")
      keyAlias System.getenv("KEY_ALIAS")
      keyPassword System.getenv("KEY_PASSWORD")
    }
  }
  defaultConfig {
    applicationId "com.test.technical.movies"
    minSdkVersion 15
    targetSdkVersion 27
    versionCode 1
    versionName "1.0"
    testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    resValue "string", "themoviedb_apiKey", System.getenv("THEMOVIEDB_APIKEY")
  }
  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
      signingConfig signingConfigs.release
    }
  }
}

task jacocoTestReport(type: JacocoReport, dependsOn: "testDebugUnitTest") {
  group = "Reporting"

  reports {
    xml.enabled = false
    html.enabled = true
  }

  def excludes = ['**/R.class',
                  '**/R$*.class',
                  '**/*$ViewInjector*.*',
                  '**/BuildConfig.*',
                  '**/Manifest*.*',
                  '**/*Test*.*',
                  'android/**/*.*',
                  '**/*Fragment*',
                  '**/*Activity*',
                  '**/*Scope*',
                  '**/*Component*',
                  '**/*Module*',
                  '**/*DataClass*',
                  '**/*Application*']
  classDirectories = fileTree(dir: "$buildDir/intermediates/classes/debug",
      excludes: excludes) + fileTree(dir: "$buildDir/tmp/kotlin-classes/debug",
      excludes: excludes)

  sourceDirectories = files([android.sourceSets.main.java.srcDirs,
                             "src/main/kotlin"])
  executionData = files("$buildDir/jacoco/testDebugUnitTest.exec")
}

dependencies {
  implementation fileTree(dir: 'libs', include: ['*.jar'])
  implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

  testImplementation 'junit:junit:4.12'
  androidTestImplementation 'com.android.support.test:runner:1.0.2'
  androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'

  implementation 'com.android.support:appcompat-v7:27.1.1'
  implementation 'com.android.support.constraint:constraint-layout:1.1.2'

  implementation 'com.squareup.okhttp3:okhttp:3.11.0'
  implementation 'com.squareup.retrofit2:retrofit:2.4.0'
  implementation 'com.squareup.retrofit2:converter-gson:2.4.0'
  implementation 'com.jakewharton.retrofit:retrofit2-rxjava2-adapter:1.0.0'

  implementation 'io.reactivex.rxjava2:rxjava:2.1.10'
  implementation 'io.reactivex.rxjava2:rxandroid:2.0.2'

  implementation 'com.google.dagger:dagger:2.14.1'
  kapt "com.google.dagger:dagger-compiler:2.14.1"

  implementation "android.arch.lifecycle:extensions:1.1.1"
  testImplementation "android.arch.core:core-testing:1.1.1"
  implementation "android.arch.lifecycle:reactivestreams:1.1.1"
  kapt "android.arch.lifecycle:compiler:1.1.1"
  implementation "android.arch.persistence.room:runtime:1.1.1"
  kapt "android.arch.persistence.room:compiler:1.1.1"
}
